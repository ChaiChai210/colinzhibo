apply plugin: 'com.android.application'

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.huaxin.shequ"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.2"
        resConfigs "zh"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //每个应用拥有不同的authorities，防止相同在同一个手机上无法同时安装
        multiDexEnabled true
        ndk {
            //选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi', 'armeabi-v7a', 'armeabi-v8a'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
        }

    }
    //签名
    signingConfigs {
        release {
            storeFile file("../huaxinshequ.jks")
            storePassword "huaxin"
            keyAlias "huaxin"
            keyPassword "huaxin"
        }
    }
    flavorDimensions "test"//1.配置dimension
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable true
            zipAlignEnabled true  //是否支持zip
            signingConfig signingConfigs.release
        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled false
            zipAlignEnabled true
            debuggable true

        }

    }
    //3.配置多渠道，这里配置2个渠道：dev测试环境，prod正式环境
    productFlavors {
        dev {
            applicationId "com.huaxin.shequ.demotest"//配置测试环境的applicationid 要和正式的不一样
            resValue "string", "app_name", "测试版"//设置测试版的appname，然后记得将strings文件中的app_name删掉
//给BuildConfig类增加一个boolean类型的字段，用来标记服务器地址是否是测试环境的
            dimension "test"//配置第一步中的flavorDimensions 配置的“test”，这里我们没有额外的需求不多做配置
            resValue "string", "authorities", applicationId
            manifestPlaceholders = rootProject.ext.dev_manifestPlaceholders
        }
        prod {
            applicationId "com.huaxin.shequ"
//            resValue "string", "app_name", "正式版"
            resValue "string", "authorities", applicationId
            manifestPlaceholders = rootProject.ext.prod_manifestPlaceholders
            dimension "test"
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                outputFileName = defaultConfig.applicationId.subSequence(defaultConfig.applicationId.lastIndexOf(".") + 1, defaultConfig.applicationId.length()) + "-v" + defaultConfig.versionName + "-" + releaseTime() + "-" + output.baseName + ".apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation project(path: ':library')
    implementation project(path: ':main')
    implementation project(path: ':usercenter')
    implementation project(path: ':community')
    implementation project(path: ':video')
}
